# Examples for small and large numbers.
```scheme
#lang sicp

(define (sqrt-iter guess x)
  (display guess)
  (display "\n")
  (if (good-enough? guess x)
      guess
      (sqrt-iter (improve guess x)
                 x)))

(define (improve guess x)
  (average guess (/ x guess)))

(define (average x y)
  (/ (+ x y) 2))

(define (good-enough? guess x)
  (< (abs (- (square guess) x)) 0.001))

(define (sqrt x)
  (sqrt-iter 1.0 x))

(define (square x)
  (* x x))

(sqrt (square 1e-5))
(sqrt 10000000000000)
```
# What I observed
When I run `(sqrt (square 1e-5))`, it gives `0.03125000106562499`, far from the true result(which is 1e-5).
When I run `(sqrt 10000000000000)`, it never ends. That's why I added `display` lines to check where does it stick. It stuck at the guess of `3162277.6601683795`.

# An alternative strategy
```scheme
#lang sicp

(define (sqrt-iter guess x)
  (if (new-good-enough? guess (improve guess x))
      guess
      (sqrt-iter (improve guess x)
                 x)))

(define (improve guess x)
  (average guess (/ x guess)))

(define (average x y)
  (/ (+ x y) 2))

(define (new-good-enough? guess next-guess)
  (< (/ (abs (- guess next-guess)) guess) 0.001))

(define (sqrt x)
  (sqrt-iter 1.0 x))

(define (square x)
  (* x x))

(sqrt (square 1e-5))
(sqrt 100000000000000)
```
# The result of new implementation
`1.0000558643074985e-5`   
`3162433.547242504`   
Absolutely this works better than before.