# $p'$ and $q'$
Given formula is
$$\begin{split}
a & \leftarrow bq+aq+ap\\
b & \leftarrow bp+aq
\end{split}$$

When we apply this twice, it becomes
$$\begin{split}
a \leftarrow & (bp+aq)q+(bq+aq+ap)q+(bq+aq+ap)p\\
& = bpq + aq^2 + bq^2 + aq^2 + apq + bqp + aqp + ap^2\\
& = b(q^2+2pq) + a(q^2 + 2pq) + a(p^2 + q^2)\\
b \leftarrow &(bp+aq)p+(bq+aq+ap)q\\
& = bp^2 + apq + bq^2 + aq^2 + apq \\
& = b(p^2 + q^2) + a(q^2+2pq)
\end{split}$$
Therefore, we get the result
$$ \begin{split}
p' = & p^2 + q^2 \\
q' = & q^2 + 2pq
\end{split}$$

# Code
```scheme
#lang sicp

(define (fib n)
  (fib-iter 1 0 0 1 n))

(define (square x)
  (* x x))

(define (p-prime p q)
  (+ (square p)
     (square q)))

(define (q-prime p q)
  (+ (square q)
     (* 2 p q)))

(define (fib-iter a b p q count)
  (cond ((= count 0) 
         b)
        ((even? count)
         (fib-iter a
                   b
                   (p-prime p q)
                   (q-prime p q)
                   (/ count 2)))
        (else 
         (fib-iter (+ (* b q) 
                      (* a q) 
                      (* a p))
                   (+ (* b p) 
                      (* a q))
                   p
                   q
                   (- count 1)))))
```
I've implemented `p-prime` and `q-prime` that generates $p'$ and $q'$   
`(fib 1000)` returns `43466557686937456435688527675040625802564660517371780402481729089536555417949051890403879840079255169295922593080322634775209689623239873322471161642996440906533187938298969649928516003704476137795166849228875` which agrees with http://homepage.cs.uiowa.edu/~sriram/21/fall07/code/FastFibonacci.out